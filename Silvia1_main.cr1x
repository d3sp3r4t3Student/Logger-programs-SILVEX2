'=========================================================================
' Programm  fuer HEFEXII Logger - Box3
'=========================================================================
'
' Patricia Asemann and Max Sesselmann adapted from
' Michael Haugeneder adapted from Franz Herzog and Chasper Buchli
'                 
'  sensors
'  ----------------------
'  upper CSAT3B (former T2 upper CSAT) (CPI)
'  lower CSAT3B (CPI)
'  CSAT3 (SDM)
'  Tair ventilated
'  CMP3 Pyranometer
'  Young Anemometer
'
'  description
'  ---------------------
' - stations name and some calibration parameters are configured in a
'   separate file called Silvia1_system_settings.cr1x
' - to prevent skipped scans with the higher scan rate, the compile mode is
'   forced to pipeline mode
' - CSAT3B CSAT3 measured in the main (fast) scan
' - ventilated Tair is measured in the slow scan
'
'**************************************************************************

PipelineMode             'run logger in pipeline mode, not in sequential mode

'***   constants & configuration settings declared in a separate file   **************
Include "CPU:Silvia1_system_settings.cr1x"   'include separate file

'***   general variables   ***********************************************************
Const SensorsStartUpDelay = 30 'wait at program start for sensors to be ready [s]

' Upper CSAT3B
Public CSAT3B_upper_Vals(5)
Public CSAT3B_upper_MonitorVals(4)
Alias CSAT3B_upper_Vals(1) = Ux_upper_CSAT3B
Alias CSAT3B_upper_Vals(2) = Uy_upper_CSAT3B
Alias CSAT3B_upper_Vals(3) = Uz_upper_CSAT3B
Alias CSAT3B_upper_Vals(4) = Ts_upper_CSAT3B
Alias CSAT3B_upper_Vals(5) = Diag_upper_CSAT3B
Alias CSAT3B_upper_MonitorVals(1) = BoardTemp_upper_CSAT3B
Alias CSAT3B_upper_MonitorVals(2) = BoardHumidity_upper_CSAT3B
Alias CSAT3B_upper_MonitorVals(3) = InclinePitch_upper_CSAT3B
Alias CSAT3B_upper_MonitorVals(4) = InclineRoll_upper_CSAT3B
Units Ux_upper_CSAT3B = m/s
Units Uy_upper_CSAT3B = m/s
Units Uz_upper_CSAT3B = m/s
Units Ts_upper_CSAT3B = C
Units Diag_upper_CSAT3B = arb
Units BoardTemp_upper_CSAT3B = C
Units BoardHumidity_upper_CSAT3B = percent
Units InclinePitch_upper_CSAT3B = deg
Units InclineRoll_upper_CSAT3B = deg

' Lower CSAT3B
Public CSAT3B_lower_Vals(5)
Public CSAT3B_lower_MonitorVals(4)
Alias CSAT3B_lower_Vals(1) = Ux_lower_CSAT3B
Alias CSAT3B_lower_Vals(2) = Uy_lower_CSAT3B
Alias CSAT3B_lower_Vals(3) = Uz_lower_CSAT3B
Alias CSAT3B_lower_Vals(4) = Ts_lower_CSAT3B
Alias CSAT3B_lower_Vals(5) = Diag_lower_CSAT3B
Alias CSAT3B_lower_MonitorVals(1) = BoardTemp_lower_CSAT3B
Alias CSAT3B_lower_MonitorVals(2) = BoardHumidity_lower_CSAT3B
Alias CSAT3B_lower_MonitorVals(3) = InclinePitch_lower_CSAT3B
Alias CSAT3B_lower_MonitorVals(4) = InclineRoll_lower_CSAT3B
Units Ux_lower_CSAT3B = m/s
Units Uy_lower_CSAT3B = m/s
Units Uz_lower_CSAT3B = m/s
Units Ts_lower_CSAT3B = C
Units Diag_lower_CSAT3B = arb
Units BoardTemp_lower_CSAT3B = C
Units BoardHumidity_lower_CSAT3B = percent
Units InclinePitch_lower_CSAT3B = deg
Units InclineRoll_lower_CSAT3B = deg

' CSAT3
Public CSAT3_Vals(5)
Alias CSAT3_Vals(1) = Ux_CSAT3
Alias CSAT3_Vals(2) = Uy_CSAT3
Alias CSAT3_Vals(3) = Uz_CSAT3
Alias CSAT3_Vals(4) = Ts_CSAT3
Alias CSAT3_Vals(5) = Diag_CSAT3
Units Ux_CSAT3 = m/s
Units Uy_CSAT3 = m/s
Units Uz_CSAT3 = m/s
Units Ts_CSAT3 = C
Units Diag_CSAT3 = arb

Public AirTempTC(NumberOfTC) : Units AirTempTC() = °C
  'temperatures of all thermocouples

Public AirTempPRT : Units AirTempPRT = °C                'reference temperature
Public PRT_resistance : Units PRT_resistance = Ω         'resistance of PRT
'Public VentilatorSpeed : Units VentilatorSpeed = Hz
  'speed of fan (actually its frequency) from ventilated radiation shield
  
'' Young anemometer variables
'Public WS_ms 'wind speed in m/s
'Public WindDir 'wind dir in degrees
'Units WS_ms = m/s
'Units WindDir = Degrees

'Pyranometer variable
Public CMP3_Rad  ' Solar radiation in W/m²

Public LoggerTemp : Units LoggerTemp = °C      'system variables
Public BatteryVoltage : Units BatteryVoltage = V
Public BatteryVoltage_min : Units BatteryVoltage_min = V
Const BattMin_RunNr = BattMin_Time * 60 / SlowScanInterval
                                               'number of values for running minimum
Public Logger As String * 50                   'variables to capture status infos
Public ProgramName As String * 50
Public ProgStartTime As Long
Public ProgramSignature, ProgramErrors, CalibErrors
Public BackupBattery : Units BackupBattery = V
Public CardsFreeMemory As Long : Units CardsFreeMemory = B

'***   constants & variables for table and file handling  ****************************
Const TableName_sonics = "Silvia1_sonics"
Const FileName_sonics = "CRD:" & TableName_sonics & "_"
Const FileTimeOnInt_sonics = 0    'new files at midnight
Const FileInterval_sonics = 6     'new file every 6 hours
Public LastWrittenFile_sonics As String * 40

Const TableName_TC = "Silvia1_TC"
Const FileName_TC = "CRD:" & TableName_TC & "_"
Public LastWrittenFile_TC As String * 40
Const FileInterval_TC = 6      '[h], every 6h create a new file
Const FileTimeOnInt_TC = 0      'new file at midnight

Const TableName_CSAT3B_upper_Monitor = "S1_CSAT3B_upper_mon"
Const FileName_CSAT3B_upper_Monitor = "CRD:" & TableName_CSAT3B_upper_Monitor & "_"
Const FileTimeOnInt_CSAT3B_upper_Monitor = 0    'new files at midnight
Const FileInterval_CSAT3B_upper_Monitor = 24     'new file every 24 hours
Public LastWrittenFile_CSAT3B_upper_Monitor As String * 40

Const TableName_CSAT3B_lower_Monitor = "S1_CSAT3B_lower_mon"
Const FileName_CSAT3B_lower_Monitor = "CRD:" & TableName_CSAT3B_lower_Monitor & "_"
Const FileTimeOnInt_CSAT3B_lower_Monitor = 0    'new files at midnight
Const FileInterval_CSAT3B_lower_Monitor = 24     'new file every 24 hours
Public LastWrittenFile_CSAT3B_lower_Monitor As String * 40

Const TableName_PRTData = "Silvia1_AirTemp_vent"
Const FileName_PRTData = "CRD:" & TableName_PRTData & "_"
Public LastWrittenFile_PRTData As String * 40
Const FileInterval_AirTemp = 24      '[h], every 24h create a new file
Const FileTimeOnInt_AirTemp = 0      'new file at midnight

'Const TableName_YoungAnem = "Silvia1_YoungAnem"
'Const FileName_YoungAnem = "CRD:" & TableName_YoungAnem & "_"
'Public LastWrittenFile_YoungAnem As String * 40
'Const FileInterval_YoungAnem = 24      '[h], every 24h create a new file
'Const FileTimeOnInt_YoungAnem = 0      'new file at midnight

Const TableName_Pyr = "Silvia1_Pyr"
Const FileName_Pyr = "CRD:" & TableName_Pyr & "_"
Public LastWrittenFile_Pyr As String * 40
Const FileInterval_Pyr = 24      '[h], every 24h create a new file
Const FileTimeOnInt_Pyr = 0      'new file at midnight

Const TableName_Service = "Silvia1_ServiceData"
Const FileName_Service = "CRD:" & TableName_Service & "_"
Const FileInterval_Service = 1       '[day], every week create a new file
Const FileTimeOnInt_Service = 0      'new file every Monday
Const TableSize_Service = Ceiling (FileInterval_Service * 24 * 60 / ServiceDataInterval * 1.2)
  'file interval [d] & data interval [min], + 20% reserve
Const MaxFiles_Service = 30          'allocate memory for 30 days, one month
Public LastWrittenFile_Service As String * 40

Const TableName_Status = "Silvia1_StationStat"
Const FileName_Status = "CRD:" & TableName_Status & "_"
Const MaxLines_Status = 100          'create a new file after 100 records
Public LastWrittenFile_Status As String * 40
Const StatTableInterval = 24        'interval of status-table called [h], daily
Const StatTableTimeOnInt = 0        'time into interval [h], midnight

'***   data tables   *****************************************************************
DataTable (TableName_sonics,True,-1) 
    'this table contains all data from the
    '  CSAT3Bs and the CSAT3
  ' CSAT3B Upper
  Sample(1, CSAT3B_upper_Vals(1), IEEE4)
  FieldNames("CSAT3B_Upper_Ux:Upper CSAT3B Ux [m/s]")
  Sample(1, CSAT3B_upper_Vals(2), IEEE4)
  FieldNames("CSAT3B_Upper_Uy:Upper CSAT3B Uy [m/s]")
  Sample(1, CSAT3B_upper_Vals(3), IEEE4)
  FieldNames("CSAT3B_Upper_Uz:Upper CSAT3B Uz [m/s]")
  Sample(1, CSAT3B_upper_Vals(4), IEEE4)
  FieldNames("CSAT3B_Upper_Ts:Upper CSAT3B TempAirSonic [°C]")
  Sample(1, CSAT3B_upper_Vals(5), IEEE4)
  FieldNames("CSAT3B_Upper_Diagnostic:Upper CSAT3B diag")
  
  ' CSAT3B Lower
  Sample(1, CSAT3B_lower_Vals(1), IEEE4)
  FieldNames("CSAT3B_Lower_Ux:Lower CSAT3B Ux [m/s]")
  Sample(1, CSAT3B_lower_Vals(2), IEEE4)
  FieldNames("CSAT3B_Lower_Uy:Lower CSAT3B Uy [m/s]")
  Sample(1, CSAT3B_lower_Vals(3), IEEE4)
  FieldNames("CSAT3B_Lower_Uz:Lower CSAT3B Uz [m/s]")
  Sample(1, CSAT3B_lower_Vals(4), IEEE4)
  FieldNames("CSAT3B_Lower_Ts:Lower CSAT3B TempAirSonic [°C]")
  Sample(1, CSAT3B_lower_Vals(5), IEEE4)
  FieldNames("CSAT3B_Lower_Diagnostic:Lower CSAT3B diag")
  
  ' CSAT3
  Sample(1, CSAT3_Vals(1), IEEE4)
  FieldNames("CSAT3_Ux:CSAT3 Ux [m/s]")
  Sample(1, CSAT3_Vals(2), IEEE4)
  FieldNames("CSAT3_Uy:CSAT3 Uy [m/s]")
  Sample(1, CSAT3_Vals(3), IEEE4)
  FieldNames("CSAT3_Uz:CSAT3 Uz [m/s]")
  Sample(1, CSAT3_Vals(4), IEEE4)
  FieldNames("CSAT3_Ts:CSAT3 TempAirSonic [°C]")
  Sample(1, CSAT3_Vals(5), IEEE4)
  FieldNames("CSAT3_Diagnostic:CSAT3 diag")
EndTable

DataTable (TableName_TC,True,-1) 
    'this table contains all data from the TCs
  DataInterval (0,FastScanInterval,mSec,0)     'samples of each fast scan
  TableFile(FileName_TC,64,-1,FileTimeOnInt_TC,FileInterval_TC,hr,-1,LastWrittenFile_TC)
  Sample (NumberOfTC,AirTempTC(1),IEEE4)
    'values from all 5 thermocouples [°C]
  FieldNames ("AirTempTC1:AirTemp TC1 [°C],AirTempTC2:tower2 AirTemp TC2 [°C],AirTempTC3:AirTemp TC3 [°C],AirTempTC4:AirTemp TC4 [°C],AirTempTC5:AirTemp TC5 [°C]")
EndTable

DataTable (TableName_PRTData,1,-1)
    'this table contains the air temperature measured with the ventilated PRT sensor
    '  its storing interval is during day the same as the
    '  slow scan interval and during night at a certain interval, set by a query in
    '  the slow scan although the table is called in the fast scan
  DataInterval (0,SlowScanInterval,sec,0)      'samples from each slow scan
  TableFile (FileName_PRTData,64,-1,FileTimeOnInt_AirTemp,FileInterval_AirTemp,hr,-1,LastWrittenFile_PRTData)
  Sample (1,AirTempPRT,IEEE4)                   'air temperature from Tvent
  FieldNames ("AirTemp:ventilated temperature reference [°C]")
EndTable

'DataTable (TableName_YoungAnem,1,-1)
'    'this table contains the wind speed and direction as measured with the Young Anemometer
'  DataInterval (0,SlowScanInterval,sec,0)      'samples from each slow scan
'  TableFile (FileName_YoungAnem,64,-1,FileTimeOnInt_AirTemp,FileInterval_AirTemp,hr,-1,LastWrittenFile_YoungAnem)
'  WindVector(1, WS_ms, WindDir, FP2, False, 0, 0, 0)
'  FieldNames("Windspeed_YoungAnem:wind speed [m/s],WindDir_YoungAnem:wind direction [deg],WindDir_std_YoungAnem:standard deviation of wind direction [deg]")
'EndTable

DataTable (TableName_Pyr,1,-1)
  DataInterval (0,SlowScanInterval,sec,0)      'samples from each slow scan
  TableFile (FileName_Pyr,64,-1,FileTimeOnInt_Pyr,FileInterval_Pyr,hr,-1,LastWrittenFile_Pyr)
  Sample (1,CMP3_Rad,IEEE4)                   'air temperature from Tvent
  FieldNames ("CMP3 Pyranometer:Incoming short wave radiation [W/m^2]")
EndTable

DataTable (TableName_Service,True,TableSize_Service)
    'this table contains the service data, such as supply voltage or recorded errors
  DataInterval (0,ServiceDataInterval,min,0)
  TableFile (FileName_Service,64,MaxFiles_Service,FileTimeOnInt_Service,FileInterval_Service,day,0,LastWrittenFile_Service)
  Sample (1,LoggerTemp,FP2)                    'loggers panel temperature
  FieldNames ("LoggerTemperature:temperature of loggers panel [°C]")
  Average (1,BatteryVoltage,FP2,False)         'stations power supply, average voltage
  FieldNames ("BatteryVoltage_avg:loggers average supply voltage [V]")
  Minimum (1,BatteryVoltage,FP2,False,False)   'minimal supply voltage (not MinRun)
  FieldNames ("BatteryVoltage_min:loggers minimum supply voltage [V]")
EndTable

DataTable (TableName_CSAT3B_upper_Monitor,True,-1)
  DataInterval (0,SlowScanInterval,sec,10)
  TableFile (FileName_CSAT3B_upper_Monitor,64,-1,FileTimeOnInt_CSAT3B_upper_Monitor,FileInterval_CSAT3B_upper_Monitor,hr,0,LastWrittenFile_CSAT3B_upper_Monitor)
  Sample (4,CSAT3B_upper_MonitorVals(1),IEEE4)
  FieldNames ("upperCSAT3B_ElectronicsTemp:temperature of electronics upperCSAT3B [°C],upperCSAT3B_ElectronicsRelHum:relative humidity of electronics upperCSAT3B [%],upperCSAT3B_TiltPitch:tilt pitch upperCSAT3B [°],upperCSAT3B_TiltRoll:tilt roll upperCSAT3B [°]")
EndTable

DataTable (TableName_CSAT3B_lower_Monitor,True,-1)
  DataInterval (0,SlowScanInterval,sec,10)
  TableFile (FileName_CSAT3B_lower_Monitor,64,-1,FileTimeOnInt_CSAT3B_lower_Monitor,FileInterval_CSAT3B_lower_Monitor,hr,0,LastWrittenFile_CSAT3B_lower_Monitor)
  Sample (4,CSAT3B_lower_MonitorVals(1),IEEE4)
  FieldNames ("lowerCSAT3B_ElectronicsTemp:temperature of electronics lowerCSAT3B [°C],lowerCSAT3B_ElectronicsRelHum:relative humidity of electronics lowerCSAT3B [%],lowerCSAT3B_TiltPitch:tilt pitch lowerCSAT3B [°],lowerCSAT3B_TiltRoll:tilt roll lowerCSAT3B [°]")
EndTable

DataTable (TableName_Status,True,MaxLines_Status)
    'this table contains several status information about the station
 ' DataInterval (0,1,day,-1)         'called manually weekly
  TableFile (FileName_Status,64,1,MaxLines_Status,0,day,0,LastWrittenFile_Status)
  Sample (1,Logger,String)
  FieldNames ("LoggerName:one from settings")
  Sample (1,ProgramName,String)
  FieldNames ("ProgramName:filename of program")
  Sample (1,ProgStartTime,NSEC)
  FieldNames ("ProgStartTime:time of program-start")
  Sample (1,ProgramSignature,UINT2)
  FieldNames ("ProgramSignature:signature of program")
  Sample (1,ProgramErrors,UINT2)
  FieldNames ("ProgramErrors:errors during compile or runtime")
  Sample (1,CalibErrors,UINT2)
  FieldNames ("CalibErrors:calibration errors")
  Sample (1,BackupBattery,FP2)
  FieldNames ("LithiumBattery:voltage of backup battery [V]")
  Sample (1,CardsFreeMemory,UINT4)
  FieldNames ("CardsFreeMemory:free memory on card [B]")
  Sample (1,LastWrittenFile_PRTData,String)
  FieldNames ("LastWrittenFile_PRTData:last file of reference temperature")
  Sample (1,LastWrittenFile_sonics, String)
  FieldNames ("LastWrittenFile_sonics: last file of sonics")
  Sample (1, LastWrittenFile_CSAT3B_upper_Monitor, String)
  FieldNames ("LastWrittenFile_CSAT3B_Monitor: last file of upper CSAT3B monitor")
EndTable

'***   function & subroutines   ******************************************************
Function GetStatus ()      'read some actual status data from Status-table
  Logger = Status.StationName
  ProgramName = Status.ProgName
  ProgStartTime = Status.StartTime
  ProgramSignature = Status.ProgSignature
  ProgramErrors = Status.ProgErrors
  CalibErrors = Status.ErrorCalib
  BackupBattery = Status.LithiumBattery
  If Status.CardStatus = "Card OK." Then       'test if a card is inserted
    CardsFreeMemory = Status.CardBytesFree     'read how much space is left on SD-card
  EndIf
EndFunction

'---   initialize loggers interfaces   -----------------------------------------------
SDMBeginPort (CSAT3_SDMport)       'configure SDM port on C5, C6 and C7

'***   main program   ****************************************************************
BeginProg

  GetStatus()              'read station status and save the startup-values
  CallTable TableName_Status

'---   switch all instruments on   ---------------------------------------------------
  PortSet (FanCtrlPwr,True,1)                  'switch fan of radiation shield on
  PortSet (CSAT3_Pwr,True,1)                   'switch power supply of CSAT3 on
  Delay (1,SensorsStartUpDelay,sec)             'wait for the instruments to be ready

'---   main scan loop   --------------------------------------------------------------
  Scan (FastScanInterval,msec,3,0)
    SW12 (SW12_1, 1, 0)                           'switch power supply of CSAT3Bs on (must be in scan loop)
    SW12 (SW12_2, 1, 0)
    
    TCDiff(AirTempTC(),NumberOfTC,mV1000,TC_DIFFch,TC_Type,AirTempPRT,True,0,15000,1,0)
    'measure all TCs one after the other, of tower 1 and tower 2, with differential
    '  measurement and automatic compensation of the the cold-junction temperature
      
    CallTable TableName_TC

    ' do EC stuff
    CSAT3B(CSAT3B_upper_Vals(), 1, CSAT3B_upper_CPIaddr, CSAT3B_Mode)
      'read the data from CSAT3B; 1 for CPI-communication
    CSAT3B(CSAT3B_lower_Vals(), 1, CSAT3B_lower_CPIaddr, CSAT3B_Mode)
      'read the data from CSAT3B; 1 for CPI-communication
    CSAT3(CSAT3_Vals(), 1, CSAT3_SDMaddr, CSAT3_Command, 20)
      'read the data from CSAT3; 0 for SDM-communication
      
    CallTable TableName_sonics
    
    'Young Anemometer wind speed measurement !!!disabled to reduce scan load, otherwise SlowSequence is never called!!!
    'PulseCount(WS_ms, 1, YoungAnemWSPulseDIFF, 5, 1, 0.098, 0)
      
  NextScan

'---   slow scan loop   --------------------------------------------------------------
  SlowSequence
    Scan (SlowScanInterval,sec,0,0)

      Battery (BatteryVoltage)       'measure the voltage of the supply
      MinRun (BatteryVoltage_min,1,BatteryVoltage,BattMin_RunNr)
        'calculate the minimum of the batterys voltage, to decide if power devices on
      PanelTemp (LoggerTemp,250)     'to measure panels temp. again doesn't make sense
      
      'Tvent measurements
      BrHalf4W (PRT_resistance,1,PRT_Vrange,PRT_Vrange,PRT_DIFFch,PRT_VXch,1,PRT_Vex,True,True,0,_50Hz,PRT_Mul,0)
        'measure platinum resistance thermometer with 4 wire half bridge measurement
      AirTempPRT = PRTcalibCoeff0 + PRT_resistance * PRTcalibCoeff1 + PRT_resistance^2 * PRTcalibCoeff2
        'calculate the exact temperature with the calibration coefficients   

'      'Young Anemometer wind direction measurement !!!disabled because of a too high scan load!!!
'      BrHalf(WindDir, 1, AutoRange, YoungAnemWDSigSE, YoungAnemVXPort, 1, 2500, True,20000, 60, 355, 0)
'        'measure wind direction of the Young anemometer
'      If WindDir>=355 OR WindDir<0 Then WindDir=0
'        'Correct invalid young anemometer wind direction readings
'      
'      CallTable TableName_YoungAnem
      
      'need to include Pyranometer here
      VoltSe(CMP3_Rad,1,Pyr_Vrange,Pyr_SECh,True,0,15000,100,0) 

      CSAT3BMonitor(CSAT3B_upper_MonitorVals(), 1, CSAT3B_upper_CPIaddr)
      CSAT3BMonitor(CSAT3B_lower_MonitorVals(), 1, CSAT3B_lower_CPIaddr)      
      
      CallTable TableName_PRTData
      CallTable TableName_Pyr
      CallTable TableName_Service
      CallTable(TableName_CSAT3B_upper_Monitor)
      CallTable(TableName_CSAT3B_lower_Monitor)

      If IfTime (StatTableTimeOnInt,StatTableInterval,Hr) Then
        GetStatus()                  'record status 
        CallTable TableName_Status
      EndIf   
    NextScan
  EndSequence
EndProg       
